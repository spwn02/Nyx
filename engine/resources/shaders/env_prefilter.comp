#version 460 core

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(binding = 0) uniform samplerCube u_EnvCube;
layout(rgba16f, binding = 1) uniform writeonly imageCube u_Prefilter;

uniform float u_Roughness = 0.0;
uniform uint u_SampleCount = 1024;

const float PI = 3.14159265359;

vec3 faceDir(uint face, vec2 uv) {
  if (face == 0u)
    return normalize(vec3(1.0, uv.y, -uv.x));
  if (face == 1u)
    return normalize(vec3(-1.0, uv.y, uv.x));
  if (face == 2u)
    return normalize(vec3(uv.x, 1.0, -uv.y));
  if (face == 3u)
    return normalize(vec3(uv.x, -1.0, uv.y));
  if (face == 4u)
    return normalize(vec3(uv.x, uv.y, 1.0));
  return normalize(vec3(-uv.x, uv.y, -1.0));
}

float radicalInverse_VdC(uint bits) {
  bits = (bits << 16u) | (bits >> 16u);
  bits = ((bits & 0x55555555u) << 1u) | ((bits & 0xAAAAAAAAu) >> 1u);
  bits = ((bits & 0x33333333u) << 2u) | ((bits & 0xCCCCCCCCu) >> 2u);
  bits = ((bits & 0x0F0F0F0Fu) << 4u) | ((bits & 0xF0F0F0F0u) >> 4u);
  bits = ((bits & 0x00FF00FFu) << 8u) | ((bits & 0xFF00FF00u) >> 8u);
  return float(bits) * 2.3283064365386963e-10;
}

vec2 hammersley(uint i, uint N) {
  return vec2(float(i) / float(N), radicalInverse_VdC(i));
}

vec3 importanceSampleGGX(vec2 Xi, vec3 N, float roughness) {
  float a = roughness * roughness;

  float phi = 2.0 * PI * Xi.x;
  float cosTheta = sqrt((1.0 - Xi.y) / (1.0 + (a * a - 1.0) * Xi.y));
  float sinTheta = sqrt(max(0.0, 1.0 - cosTheta * cosTheta));

  vec3 H;
  H.x = cos(phi) * sinTheta;
  H.y = sin(phi) * sinTheta;
  H.z = cosTheta;

  vec3 up = abs(N.z) < 0.999 ? vec3(0, 0, 1) : vec3(1, 0, 0);
  vec3 T = normalize(cross(up, N));
  vec3 B = cross(N, T);

  return normalize(T * H.x + B * H.y + N * H.z);
}

float D_GGX(float NdotH, float a) {
  float a2 = a * a;
  float d = (NdotH * NdotH) * (a2 - 1.0) + 1.0;
  return a2 / (PI * d * d);
}

void main() {
  uvec3 gid = gl_GlobalInvocationID;
  uint face = gid.z;

  ivec2 size = imageSize(u_Prefilter).xy;
  if (int(gid.x) >= size.x || int(gid.y) >= size.y)
    return;

  vec2 uv = (vec2(gid.xy) + 0.5) / vec2(size);
  uv = uv * 2.0 - 1.0;

  vec3 R = faceDir(face, uv);
  vec3 N = R;
  vec3 V = R;

  float rough = clamp(u_Roughness, 0.0, 1.0);
  float a = max(0.001, rough * rough);

  vec3 prefiltered = vec3(0.0);
  float totalW = 0.0;

  // Mip level from roughness is usually used, but since we *write* to mips,
  // we can just sample base env cube (lod 0) here for simplicity.
  // Later: compute lod from pdf + solid angle for higher quality.
  for (uint i = 0u; i < u_SampleCount; ++i) {
    vec2 Xi = hammersley(i, u_SampleCount);
    vec3 H = importanceSampleGGX(Xi, N, rough);
    vec3 L = normalize(reflect(-V, H));

    float NdotL = max(dot(N, L), 0.0);
    if (NdotL > 0.0) {
      vec3 c = textureLod(u_EnvCube, L, 0.0).rgb;
      prefiltered += c * NdotL;
      totalW += NdotL;
    }
  }

  prefiltered = (totalW > 0.0) ? (prefiltered / totalW) : vec3(0.0);
  imageStore(u_Prefilter, ivec3(gid.xy, int(face)), vec4(prefiltered, 1.0));
}
