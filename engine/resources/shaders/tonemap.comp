#version 460 core
layout(local_size_x = 16, local_size_y = 16) in;

layout(binding = 0) uniform sampler2D u_HDR;
layout(rgba8, binding = 1) uniform writeonly image2D u_LDR;

uniform float u_Exposure = 1.0;
uniform int u_ApplyGamma = 1;

vec3 acesFitted(vec3 x) {
  // Narkowicz ACES approximation (fast, looks good)
  // https://knarkowicz.wordpress.com/2016/01/06/aces-filmic-tone-mapping-curve/
  const float a = 2.51;
  const float b = 0.03;
  const float c = 2.43;
  const float d = 0.59;
  const float e = 0.14;
  return clamp((x * (a * x + b)) / (x * (c * x + d) + e), 0.0, 1.0);
}

void main() {
  ivec2 pix = ivec2(gl_GlobalInvocationID.xy);
  ivec2 sz = imageSize(u_LDR);
  if (pix.x >= sz.x || pix.y >= sz.y)
    return;

  vec3 hdr = texelFetch(u_HDR, pix, 0).rgb;

  // Exposure (stub, later from project/view settings)
  hdr *= max(u_Exposure, 0.0);

  // Tonemap
  vec3 ldr = acesFitted(hdr);

  if (u_ApplyGamma != 0) {
    ldr = pow(ldr, vec3(1.0 / 2.2));
  }

  imageStore(u_LDR, pix, vec4(ldr, 1.0));
}
