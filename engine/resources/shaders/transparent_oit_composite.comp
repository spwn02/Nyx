#version 460 core
layout(local_size_x = 16, local_size_y = 16) in;

layout(binding = 0) uniform sampler2D uBaseHDR;
layout(binding = 1) uniform sampler2D uAccum;
layout(binding = 2) uniform sampler2D uReveal;

layout(rgba16f, binding = 3) uniform writeonly image2D uOutHDR;

void main() {
  ivec2 pix = ivec2(gl_GlobalInvocationID.xy);
  ivec2 sz = imageSize(uOutHDR);
  if (pix.x >= sz.x || pix.y >= sz.y)
    return;

  vec4 base = texelFetch(uBaseHDR, pix, 0);
  vec4 accum = texelFetch(uAccum, pix, 0);
  float reveal = texelFetch(uReveal, pix, 0).r;

  if (accum.a <= 1e-6) {
    imageStore(uOutHDR, pix, base);
    return;
  }

  vec3 avg = accum.rgb / max(accum.a, 1e-6);
  float a = clamp(1.0 - reveal, 0.0, 1.0);

  vec3 outRgb = base.rgb * (1.0 - a) + avg * a;
  imageStore(uOutHDR, pix, vec4(outRgb, 1.0));
}
